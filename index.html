<html>
  <head>
    <title>Aquarium Simulator</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <link rel="stylesheet" href="css/reset.css" type="text/css">



    <!-- Fragment shader program -->
    <script id="sandboxFragmentShader" type="x-shader/x-fragment">
	#ifdef GL_ES
	precision mediump float;
	#endif

	uniform float time;
	uniform vec2 resolution;

	void main( void ) {
		// background thing
		vec2 position = ( gl_FragCoord.xy / resolution.xy * 2.0) - 1.0;
		vec4 veca = vec4((mod(atan(position.x ,position.y) / 1.6, 0.1) * 15.0 + 0.5) * 0.6);
		vec4 vecb = vec4(0.2, sin(time), 0.3, 1.0) * (1.5- length(position));
		float l = (length(vec2(position.x , position.y)));
		vec4 color = veca * vecb * l;

		// spinny thing
		float th = atan(position.y, position.x) + time*0.5;
		th = mod(th, 3.14159*0.125);

		// color things
		float rmod = th * 0.2;
		float gmod = th * 0.5;
		float bmod = th * 1.0;

		gl_FragColor = vec4(color.x + rmod, color.y + gmod, color.z + bmod, 1.0);
	}
    </script>

    <!-- Vertex shader program -->
    <script id="sandboxVertexShader" type="x-shader/x-vertex">
      attribute vec3 position;

      uniform mat4 modelview, projection;

      void main(void) {
        gl_Position = projection * modelview * vec4(position, 1.0);
      }
    </script>

    <script id="3DfragmentShader" type="x-shader/x-fragment">
    	#ifdef GL_ES
		precision mediump float;
		#endif

		varying vec3 Position;
		varying vec3 Normal;
		varying vec2 TexCoord;

		uniform sampler2D Tex1;

		struct LightInfo {
		  vec4 LightPosition;  // Light position in eye coords.
		  vec3 LightIntensity; // A,D,S intensity
		  bool LightEnabled;
		};
		uniform LightInfo Light;

		struct MaterialInfo {
		  vec3 Ka;            // Ambient reflectivity
		  vec3 Kd;            // Diffuse reflectivity
		  vec3 Ks;            // Specular reflectivity
		  float Shininess;    // Specular shininess factor
		};
		uniform MaterialInfo Material;

		void phongModel( vec3 pos, vec3 norm, out vec3 ambAndDiff, out vec3 spec ) {
		    vec3 s = normalize(vec3(Light.LightPosition));
		    vec3 v = normalize(-pos.xyz);
		    vec3 r = reflect(-s, norm);
		    vec3 ambient = Light.LightIntensity * Material.Ka;
		    float sDotN = max(dot(s,norm), 0.0);
		    vec3 diffuse = Light.LightIntensity * Material.Kd * sDotN;
		    spec = vec3(0.0);
		    if(sDotN > 0.0)
		        spec = Light.LightIntensity * Material.Ks *
		               pow(max(dot(r,v), 0.0), Material.Shininess);
		    ambAndDiff = ambient + diffuse;
		}

		void main() {
		    vec4 texColor = texture2D(Tex1, TexCoord);
		    //vec4 texColor = vec4(0.0, 0.3, 0.0, 1.0);

		    if(Light.LightEnabled){
			    vec3 ambAndDiff, spec;
			    phongModel( Position, Normal, ambAndDiff, spec );
			    gl_FragColor = (vec4( ambAndDiff, 1.0 ) * texColor) + vec4(spec,1.0);
		    } else{
		    	/*
		    	 * For efficiency, only check for alpha discards
		    	 * when lighitng is turned off
		    	 * (otherwise we just assume we don't want
		    	 * transparency)
		    	 */
		        if(texColor.w == 0.0)
		    		discard;
		    	gl_FragColor = (texColor);
		    }
		}
    </script>

    <script id="3DvertexShader" type="x-shader/x-vertex">
		attribute vec3 VertexPosition;
		attribute vec3 VertexNormal;
		attribute vec2 VertexTexCoord;

		// position in world coordinates (for lighting)
		varying vec3 Position;
		varying vec3 Normal;
		varying vec2 TexCoord;

		uniform mat4 ModelViewMatrix;
		uniform mat4 ProjectionMatrix;
		uniform mat3 NormalMatrix;

		void main()
		{
		    TexCoord = vec2(VertexTexCoord.x, VertexTexCoord.y);
		    Normal = normalize(NormalMatrix * VertexNormal);
		    Position = vec3(ModelViewMatrix * vec4(VertexPosition,1.0));

			mat4 MVP = ProjectionMatrix * ModelViewMatrix;
		    gl_Position = MVP * vec4(VertexPosition,1.0);
		}
    </script>

    <script src="js/jquery-1.10.0.js" type="text/javascript"></script>

    <script src="js/sylvester.js" type="text/javascript"></script>
    <script src="js/glUtils.js" type="text/javascript"></script>

    <script src="js/modelloader.js" type="text/javascript"></script>
    <script src="js/modelrenderer.js" type="text/javascript"></script>
    <script src="js/textureloader.js" type="text/javascript"></script>
    <script src="js/sandbox.js" type="text/javascript"></script>
    <script src="js/objects.js" type="text/javascript"></script>
    <script src="js/renderer.js" type="text/javascript"></script>
    <script src="js/Camera.js" type="text/javascript"></script>
    <script src="js/KeyBoardEvents.js" type="text/javascript"></script>
    <script src="js/runner.js" type="text/javascript"></script>

  </head>

  <body onload="start()"></body>
</html>
